cmake_minimum_required(VERSION 3.18)
project(SampleProject)

#[[
	The following snippet looks for project.cmake anywhere in the primary
	build directory.

	This sample project builds in primary-build-dir/sample, but it is not
	part of the primary project. It is built separately, but looks for
	the package generated by the primary project.

	project.cmake is generated by the primary project as part of its
	distribution package. It mostly contains target exports.

	- ${PROJECT_BINARY_DIR} = primary-build-dir/sample
	- ${PROJECT_BINARY_DIR}/../ = primary-build-dir

	When CPack generates a ZIP package, it generates a real ZIP file, but it
	also generates an unzipped directory which contains the same files.

	Thus, search the primary build directory for project.cmake, which will
	reside in this unzipped package directory.
]]
file(GLOB_RECURSE project_exports "${PROJECT_BINARY_DIR}/../project.cmake")
list(FILTER project_exports INCLUDE REGEX "ZIP")
list(LENGTH project_exports project_exports_length)
if(NOT project_exports_length EQUAL 1)
	message(FATAL_ERROR "Cannot locate project.cmake")
endif()
include(${project_exports})

get_filename_component(exports_dir "${project_exports}" DIRECTORY)
set(package_root "${exports_dir}/..")
set(CMAKE_MSVCIDE_RUN_PATH "${package_root}/bin")

add_executable(main WIN32 MACOSX_BUNDLE main.cxx)
add_custom_target(run main)

target_link_libraries(main
	PRIVATE
		project::project
		project::SDL2
		project::SDL2main
)
